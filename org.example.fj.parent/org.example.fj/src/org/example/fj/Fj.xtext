grammar org.example.fj.FJ with org.eclipse.xtext.common.Terminals

generate fj "http://www.example.org/fj/FJ"

FJProgram :
	(classes += FJClass)*
	(main = FJExpression)? 
;

FJClass:
	'class' name=ID ('extends' superclass=[FJClass])? '{'
		(members += FJMember)*
	'}'
;

FJMember: FJField | FJMethod;

enum FJAccessLevel:
	PRIVATE='private' | PROTECTED='protected' | PUBLIC='public';

FJField:
	access=FJAccessLevel? type=[FJClass] name=ID ';'
;

FJMethod:
	access=FJAccessLevel?
	type=[FJClass] name=ID '(' (params+=FJParameter (',' params+=FJParameter)*)? ')' '{'
		'return' expression=FJExpression ';'
	'}'
;

FJParameter:
	type=[FJClass] name=ID
;

FJTypedElement:
	FJMember | FJParameter;

FJExpression:
	FJTerminalExpression 
	=>(
		{FJMemberSelection.receiver=current} '.' 
		member=[FJMember]
		(methodinvocation?='('
			(args+=FJExpression (',' args+=FJExpression)*)? ')'
		)?
	)*
;

FJTerminalExpression returns FJExpression:
   FJThis |
   FJSuper |
   FJParamRef |
   FJNew |
   =>FJCast |
   '(' FJExpression ')'
;

FJThis: {FJThis} 'this';
FJSuper: {FJSuper} 'super';
FJParamRef: parameter=[FJParameter];
FJNew: 'new' type=[FJClass] '(' (args+=FJExpression (',' args+=FJExpression)*)? ')';
FJCast: '(' type=[FJClass] ')' expression=FJExpression;
